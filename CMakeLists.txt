CMAKE_MINIMUM_REQUIRED(VERSION 2.6)
PROJECT(nnstreamer-edge)

INCLUDE(FindPkgConfig)

OPTION(ENABLE_TEST      "Enable Test case" OFF)
OPTION(ENABLE_DEBUG     "Enable Debug" OFF)

IF (NOT DEFINED VERSION)
    SET(VERSION    0.1.0)
ENDIF()

# GoogleTest requires at least C++11 and match the nnstreamer cpp version.
SET(CMAKE_CXX_STANDARD 14)

# Set CFLAGS
SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wall -Werror")
SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${EXTRA_CFLAGS} -pthread -fPIE -fPIC -g")

IF (ENABLE_DEBUG)
    SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -DDEBUG=1")
ELSE()
    SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -DDEBUG=0")
ENDIF()

# Set as-needed option
SET(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} -Wl,--as-needed")
SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -Wl,--as-needed")

SET(SO_VERSION    ${VERSION})
SET(PREFIX        ${CMAKE_INSTALL_PREFIX})
SET(EXEC_PREFIX   ${PREFIX}/bin)
SET(LIB_INSTALL_DIR ${CMAKE_INSTALL_LIBDIR})
SET(INCLUDE_INSTALL_DIR "${PREFIX}/include")

SET(SOURCE_DIR    ${CMAKE_CURRENT_SOURCE_DIR}/src)
SET(INCLUDE_DIR   ${CMAKE_CURRENT_SOURCE_DIR}/include)
SET(NNS_EDGE_SRC_DIR  ${SOURCE_DIR}/libnnstreamer-edge)

# Paho MQTT Library
SET(ENABLE_PAHO_MQTT OFF)

PKG_SEARCH_MODULE(PAHO_MQTT_PKG "paho-mqtt-c")
IF(PAHO_MQTT_PKG_FOUND)
    MESSAGE("Found Paho MQTT pkgconfig.")
    SET(ENABLE_PAHO_MQTT ON)
    SET(PAHO_MQTT_LIB ${PAHO_MQTT_PKG_LDFLAGS})
ELSE()
    FIND_LIBRARY(PAHO_MQTT_LIB NAMES paho-mqtt3a paho-mqtt3c paho-mqtt3as paho-mqtt3cs)

    IF(NOT PAHO_MQTT_LIB)
        MESSAGE("Cannot find Paho MQTT library." )
    ELSE()
        MESSAGE("Found Paho MQTT library.")
        SET(ENABLE_PAHO_MQTT ON)
    ENDIF()
ENDIF()

IF(ENABLE_PAHO_MQTT)
    SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -DENABLE_MQTT=1")
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -DENABLE_MQTT=1")
ENDIF()

# Check requires packages
# TODO FIXME remove glib dependency
SET(REQUIRES_LIST "glib-2.0 gio-2.0")

PKG_CHECK_MODULES(EDGE_REQUIRE_PKGS REQUIRED ${REQUIRES_LIST})

FOREACH(flag ${EDGE_REQUIRE_PKGS_CFLAGS})
    SET(EXTRA_CFLAGS "${EXTRA_CFLAGS} ${flag}")
ENDFOREACH(flag)

ADD_SUBDIRECTORY(src)

IF (ENABLE_TEST)
    SET(TEST_REQUIRES_LIST gtest)
    PKG_CHECK_MODULES(TEST_REQUIRE_PKGS REQUIRED ${TEST_REQUIRES_LIST})
    ADD_SUBDIRECTORY(tests)
ENDIF()

# pkgconfig file
CONFIGURE_FILE(nnstreamer-edge.pc.in nnstreamer-edge.pc @ONLY)
INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/nnstreamer-edge.pc
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/pkgconfig)
